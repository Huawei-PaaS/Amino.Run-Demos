apply plugin: 'com.android.application'
android {
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    defaultConfig {
        applicationId "com.example.hankstodo"
        versionCode 1
        versionName "1.0"
        minSdkVersion 26
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    dexOptions {
        preDexLibraries = false
        additionalParameters = ["--core-library"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.amino-os.run:amino-run-core:1.0.0.RC6'
}

subprojects {
    genStubs {
        def pkgName = 'amino.run.appexamples.hankstodo.app'
        def src = "$projectDir.parent/build/intermediates/javac/debug/compileDebugJavaWithJavac/classes/amino/run/appexamples/hankstodo/app/"
        def dst = "$projectDir.parent/src/main/java/amino/run/appexamples/hankstodo/app/stubs/"
        args src, pkgName, dst
        outputs.dir dst
    }

    dockerfile {
        copyFile './build/libs/hanksTodo.jar', '/root/amino/jars/'
    }

    buildDockerImage {
        tag =  "$docker.registryCredentials.username/hankstodo:latest"
    }

    pushImage {
        imageName = "$docker.registryCredentials.username/hankstodo"
        tag = 'latest'
    }

    createOMSDeployment {
        expand(image: "$docker.registryCredentials.username/hankstodo:latest",
                svcport: project.property('omssvcport'),
                rmiport: project.property('omsrmiport'),
                appname: project.property('appname'))
    }

    createKernelServerDeployment {
        expand(image: "$docker.registryCredentials.username/hankstodo:latest",
                omssvcport: project.property('omssvcport'),
                svcport: project.property('kernelserversvcport'),
                rmiport: project.property('kernelserverrmiport'),
                svcport2: project.property('kernelserver2svcport'),
                rmiport2: project.property('kernelserver2rmiport'),
                appname: project.property('appname'))
    }


    fatJar {
        manifest {
            attributes 'Implementation-Title': 'HanksToDo fat jar',
                    'Implementation-Version': '1.0',
                    'Main-Class': 'amino.run.appexamples.hankstodo.device.TodoActivity'
        }
    }
}

